;{anim} = curve min(animate2({data}, length, delay, turn on time, turn off time), 1)

{sneaking} = {data_1} 
{idle} = sin(pi{data_2})
{aim} = {data_3}
{gas} = {data_5}

{movement} = cos({limbSwing} * 0.6662 + pi) * 2.0 * {limbSwingAmount} * 0.5

{head_rotX} -= min({lookY}, 0.7) + 0.3{sneaking}
{head_rotY} -= 0.7*({bodyYaw} - {yaw})'

{body_rotX} += 0.3{sneaking}

{rLeg_rotX} -= {movement} + 0.3{sneaking} 
{lLeg_rotX} += {movement} - 0.3{sneaking}

{punch} = {data_0}
{f6} = {punch}
{body_rotY} = sin(sqrt({f6}) * pi * 2.0) * 0.1
{head_rotY} -= sin(sqrt({f6}) * pi * 2.0) * 0.1
{brY} = sin(sqrt({f6}) * pi * 2.0) * 0.2

{rArm_posX} += -cos({brY})+1
{rArm_posZ} += sin({brY}) * 5.0

{lArm_posX} += cos({brY})-1
{lArm_posZ} += -sin({brY}) * 5.0

{rArm_rotY} += {brY}
{lArm_rotX} += {brY}
{lArm_rotY} += {brY}

{f6} = 1.0 - {punch}
{f6} *= {f6}
{f6} *= {f6}
{f6} = 1.0 - {f6}
{f7} = sin({f6} * pi)
{f8} = sin({punch} * pi) * -({bipedHead_rotX_rotX} - 0.7) * 0.75
{rArm_rotX} = ({rArm_rotX} - ({f7} * 1.2 + {f8}))
{rArm_rotY} += {brY} * 2.0
{rArm_rotZ} = sin({punch} * pi) * -0.4

{rArm_rotX} += {movement}
{rArm_rotZ} += 0.1{idle}

{lArm_rotX} -= {movement}
{lArm_rotZ} -= 0.1{idle} 
;aiming
{aim} = curve {aim}
{rArm_rotX} @ {aim} -> {head_rotX} - pi / 2
{rArm_rotY} @ {aim} -'> {head_rotY}
{rArm_rotZ} @ {aim} -> {head_rotZ}

{lArm_rotX} @ {aim} -> {head_rotX} - pi / 2
{lArm_rotY} @ {aim} -'> {head_rotY}
{lArm_rotZ} @ {aim} -> {head_rotZ}

;Flight=====================================================================
; data_6 = flight_timer
; data_7 = flight_boost_timer
; data_8 = scale

{data_6} = curve {data_6}
{data_7} = curve {data_7}

; Boosted/stationary "base" positioning
{rArm_rotX} @ {data_6} -> 0
{rArm_rotY} @ {data_6} -'> 0
{rArm_rotZ} @ {data_6} -> 0
{lArm_rotX} @ {data_6} -> 0
{lArm_rotY} @ {data_6} -'> 0
{lArm_rotZ} @ {data_6} -> 0
{rLeg_rotX} @ {data_6} -> 0
{rLeg_rotY} @ {data_6} -'> 0
{rLeg_rotZ} @ {data_6} -> 0
{lLeg_rotX} @ {data_6} -> 0
{lLeg_rotY} @ {data_6} -'> 0
{lLeg_rotZ} @ {data_6} -> 0
{head_rotX} @ {data_7} -> -40'

; ==============================================
{data_8} = if({data_8} > 0, {data_8}^0.25, 1)
{motX} /= {data_8}
{motY} /= {data_8}
{motZ} /= {data_8}
{data_8} = 0

{vel} = sqrt({motX}^2 + {motZ}^2)
{dir} = sqrt({lookX}^2 + {lookZ}^2)

{d} = acos clamp(if({dir} > 0 && {vel} > 0, ({motX}{lookX} + {motZ}{lookZ}) / {vel} / {dir}, 1), -1, 1)
{d1} = signum({motX}{lookZ} - {motZ}{lookX})

{cosa} = cos({bodyYaw}')
{sina} = sin({bodyYaw}')
{strafe} = {motX}{cosa} + {motZ}{sina}
{forward} = {motZ}{cosa} - {motX}{sina}
{hover} = {data_6}(1 - {data_7})

out "pivot" (0, 0.56{hover}, 0)


{x} = min({vel}, 1){data_7}{d1}{d} / 2.5
{rArm_rotX} += 15'{data_7} + {x}
{rArm_rotY} += 8'{data_7}
{rArm_rotZ} += 10'{data_7}
{lArm_rotX} += 15'{data_7} - {x}
{lArm_rotY} -= 8'{data_7}
{lArm_rotZ} -= 10'{data_7}
{rLeg_rotX} += 15'{data_7} + 0.3{x}
{rLeg_rotY} += 2'{data_7}
{rLeg_rotZ} += 3'{data_7}
{lLeg_rotX} += 15'{data_7} - 0.3{x}
{lLeg_rotY} -= 2'{data_7}
{lLeg_rotZ} -= 3'{data_7}

{d} *= {data_7} / 1'
{x} = min({vel} / 2, 1) / 2
out "rotate" (-{data_7}({pitch} + 80), 1, 0, 0)
out "rotate" ({d1}{d}, 0, 1, 0)
out "rotate" (-{d1}{d}{x}, 0, 0, 1)
out "rotate" ({d}{x}, 1, 0, 0)
out "translate" (0, -0.8{data_7}, 0)

;================================

{gasPose} = min(5{gas},1)
{body_posY} += 6{gasPose}
{body_posZ} @ {gasPose} -> -6
{body_rotX} @ {gasPose} -> 30'
{body_rotY} -= 13'sin(3pi{gas})

{rLeg_rotX} @ {gasPose} -> -37.5'
{rLeg_posY} @ {gasPose} -> 7
{rLeg_posZ} @ {gasPose} -> -3
{rLeg_rotZ} += 13'sin(3pi{gas})

{lLeg_rotX} @ {gasPose} -> 22.5'

{rArm_rotX} -= 23'sin(3pi{gas})
{rArm_rotX} -= 23'sin(4pi{gas})
{rArm_rotZ} += 70'{gasPose}

{lArm_rotX} += 28'sin(3pi{gas})
{lArm_rotX} += 20'sin(4pi{gas})
{lArm_rotZ} -= 70'{gasPose}
